; svn $Id$
;+
; NAME:
;   MGH_TF_LATITUDE
;
; PURPOSE:
;   This function is designed for use with the TICKFORMAT property of IDLgrAxis.
;   It adds 'S' and 'N' suffices appropriately.
;
; CATEGORY:
;   Miscellaneous
;   Object graphics
;
; CALLING SEQUENCE:
;   Result = MGH_TF_LATITUDE(Direction, Index, Value)
;
; INPUTS:
;   Direction:  Axis direction, required by the TICKFORMAT interface but ignored.
;
;   Index:      Axis index, required by the TICKFORMAT interface but ignored.
;
;   Value:      The real value to be formatted.
;
; KEYWORD PARAMETERS:
;   DATA (input, scalar structure):
;     Specify this keyword to control the output. The value should be
;     a structure with one or more of the tags "format" and
;     "round". The default is equivalent to {format: '', round: 0}.
;
; RETURN VALUE:
;   The function returns a scalar string representing the input
;   value. If data.round is set to a 'true' value then this is
;   rounded.  The format is controlled by data.format. If this is not
;   supplied a default format is generated by FORMAT_AXIS_VALUES.
;
;###########################################################################
;
; This software is provided subject to the following conditions:
;
; 1.  NIWA makes no representations or warranties regarding the
;     accuracy of the software, the use to which the software may
;     be put or the results to be obtained from the use of the
;     software.  Accordingly NIWA accepts no liability for any loss
;     or damage (whether direct of indirect) incurred by any person
;     through the use of or reliance on the software.
;
; 2.  NIWA is to be acknowledged as the original author of the
;     software where the software is used or presented in any form.
;
;###########################################################################
;
; MODIFICATION HISTORY:
;   Mark Hadfield, Jun 1998:
;     Written.
;   Mark Hadfield, Feb 2000:
;     Added format control via the DATA keyword.
;   Mark Hadfield, May 2001:
;     Added rounding via the "round" tag in DATA. I might generalise
;     this some day.
;   Mark Hadfield, 2002-10:
;     Added rounding via the "round" tag in DATA. I might generalise
;     this some day.
;-

function MGH_TF_LATITUDE_PRIVATE_STRING, Value, Format

    compile_opt IDL2, HIDDEN

    result = strlen(format) gt 0 $
                ? string(Value, FORMAT=format) $
                : mgh_format_float(Value)

    return, result[0]

end

function MGH_TF_LATITUDE, Direction, Index, Value, DATA=data

   compile_opt DEFINT32
   compile_opt STRICTARR

   format = ''  &  round = 0

   if size(data, /TYPE) eq 8 then begin
      if n_elements(data) ne 1 then $
           message, 'The DATA structure must have one element'
      if mgh_struct_has_tag(data, 'format') then format = data.format
      if mgh_struct_has_tag(data, 'round') then round = data.round
   endif

   rvalue = round ? round(value) : value

   case 1 of
      rvalue lt 0: $
           return, mgh_tf_latitude_private_string(-rvalue, format)+'!Z(00B0)S'
      rvalue eq 0: $
           return, mgh_tf_latitude_private_string(rvalue, format)+'!Z(00B0)'
      rvalue gt 0: $
           return, mgh_tf_latitude_private_string(rvalue, format)+'!Z(00B0)N'
      else: $
           return, mgh_tf_latitude_private_string(rvalue, format)+'!Z(00B0)'
   endcase

end

